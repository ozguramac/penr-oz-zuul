group 'com.oz'
version '0.0.1-SNAPSHOT'

apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$version_spring_boot"
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
        classpath "mysql:mysql-connector-java:$version_mysql_connector"
    }
}

apply plugin: 'java'
jar {
    baseName = 'oauthsvc'
}

repositories {
    mavenCentral()
}


sourceSets {
    itest {
        java.srcDir file('src/itest/java')
        resources.srcDir file('src/main/resources')

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter:$version_spring_boot"
    compile "org.springframework.boot:spring-boot-starter-web:$version_spring_boot"
    compile "org.springframework.boot:spring-boot-starter-jetty:$version_spring_boot"
    compile "org.springframework.boot:spring-boot-starter-actuator:$version_spring_boot"
    compile "org.springframework.boot:spring-boot-starter-security:$version_spring_boot"
    compile "org.springframework.boot:spring-boot-starter-jdbc:$version_spring_boot"

    compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.7.RELEASE'

    compile "mysql:mysql-connector-java:$version_mysql_connector"

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.springframework:spring-test:4.1.6.RELEASE'
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
}

task itest (type:Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
    outputs.upToDateWhen { false }
}